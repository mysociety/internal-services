#!/usr/bin/perl -w -I../../perllib -I../../../perllib -I../
#
# scrapers/lord
# Load in Lords from JSON and write out CSV.
#
# Parameters: 
# $1 - people.json file
# $2 - output CSV file of all current members
#
# Example use:
# ./lord ~/parlparse/members/people.json ~/repdata/mysociety/lords.csv

use strict;

use File::Slurp;
use JSON;
use POSIX;
use Text::CSV_XS;

use Common;

my $today = strftime("%Y-%m-%d",localtime(time()));

my $people_json = shift(@ARGV);
die "specify people.json file" unless defined($people_json);
my $file_output_all = shift(@ARGV);
die "specify output CSV" if (!defined($file_output_all));

my $j = decode_json(read_file($people_json));
my (%orgs, %persons);
$orgs{$_->{id}} = $_ for @{$j->{organizations}};
$persons{$_->{id}} = $_->{other_names} for @{$j->{persons}};

my $nowc = 0;
my @membersnow;
foreach my $member (@{$j->{memberships}}) {
    next unless $member->{organization_id} && $member->{organization_id} eq 'house-of-lords';
    next unless $member->{start_date} le $today && end_date($member) ge $today;
    $nowc++;
    push @membersnow, $member;
    $member->{party} = $member->{on_behalf_of_id} ? $orgs{$member->{on_behalf_of_id}}{name} : '';
}
die "very few Lords! only $nowc" if $nowc < 700;

# Attach names to memberships
foreach my $member (@membersnow) {
    my @names = @{$persons{$member->{person_id}}};
    @names = grep { $_->{note} eq 'Main' } @names;
    @names = reverse sort { end_date($a) cmp end_date($b) } @names;
    $member->{name} = $names[0];
    die unless $member->{name} && $member->{party};
}

my $csv = new Text::CSV_XS({ binary => 1 });
open(CSV, ">$file_output_all");
print CSV "name, constituency, email, fax, phone, constituencyfax, party\n";
foreach my $member (sort {
        return $a->{name}{lordofname} cmp $b->{name}{lordofname} if !$a->{name}{lordname} && !$b->{name}{lordname};
        return $a->{name}{lordofname} cmp $b->{name}{lordname} if !$a->{name}{lordname};
        return $a->{name}{lordname} cmp $b->{name}{lordofname} if !$b->{name}{lordname};
        return $a->{name}{lordname} cmp $b->{name}{lordname} || $a->{name}{lordofname} cmp $b->{name}{lordofname};
    } @membersnow) {
    my $fullname = $member->{name}{honorific_prefix};
    if ($member->{name}{lordname}) {
        $fullname .= " " . $member->{name}{lordname};
    }
    if ($member->{name}{lordofname}) {
        $fullname .= " of " . $member->{name}{lordofname};
        if (!$member->{name}{lordname}) {
            $fullname = "The " . $fullname;
        }
    }
    $csv->print(\*CSV, [$fullname, "House of Lords", undef, undef, undef, undef, $member->{party}]);
    print CSV "\n";
}

sub end_date {
    $_[0]->{end_date} // '9999-12-31';
}
