#!/usr/bin/perl
#
# general-election-big-red-button:
# This is the script that acts as a big red button when a general election is
# called. We don't know when that will be, but hopefully when we do, running
# this script will do the necessaries on all mySociety sites.
#
# Run on the TheyWorkForYou server as the live TheyWorkForYou user, or root.
# 
# Copyright (c) 2010 UK Citizens Online Democracy. All rights reserved.
# Email: matthew@mysociety.org; WWW: http://www.mysociety.org/

use strict;
use FindBin;
use lib "$FindBin::Bin/../../perllib";
use mySociety::Config;
use mySociety::MaPit;
use mySociety::DaDem;
mySociety::Config::set_file("$FindBin::Bin/../conf/general");

use constant NUM_CURRENT_MPS => 643;

print <<EOF;
******************************************************
* This script will:                                  *
*   Turn off all MPs on WriteToThem                  *
*   Mark all MPs as having left on TheyWorkForYou    *
*   Turn off the posting interface of HearFromYourMP *
*                                                    *
* To confirm you wish to do this, please type yes    *
* and hit return.                                    *
* You can also enter test to do a vague dry run.     *
******************************************************
EOF
my $confirm = <>;
exit unless $confirm =~ /^y(es)?$/i || $confirm =~ /^test$/i;

my $dry_run = ($confirm =~ /^test$/i);
theyworkforyou();
writetothem();
hearfromyourmp();

# Turn off MPs on TheyWorkForYou
sub theyworkforyou {
    my ($d,$m,$y) = (localtime())[3..5];
    my $date = sprintf("%d-%02d-%02d", $y+1900, $m+1, $d);

    my $out = '';
    my $count = 0;
    open(FP, '/home/twfy-live/parlparse/members/all-members.xml') or die $!;
    while (<FP>) {
        $count += s/todate="9999-12-31"(.*?)towhy="still_in_office"/todate="$date"$1towhy="general_election"/;
        $out .= $_;
    }
    close FP;
    die "Did not make " . NUM_CURRENT_MPS . " changes" unless $count == NUM_CURRENT_MPS;
    if ($dry_run) {
        print <<EOF;
******************************************************
Here is what all-members.xml will be changed to:

$out

xml2db.pl --all --members will then be called.
******************************************************
EOF
        return;
    }
    open(FP, '>/home/twfy-live/parlparse/members/all-members.xml') or die $!;
    print FP $out;
    close FP;
    system("/data/vhost/www.theyworkforyou.com/theyworkforyou/scripts/xml2db.pl --all --members");
}

# Mark all MP seats as upcoming election for WriteToThem
sub writetothem {
    my $area_ids = mySociety::MaPit::get_areas_by_type('WMC');
    die unless @$area_ids == 646; # Areas all exist, unlike MPs
    if ($dry_run) {
        print <<EOF;
******************************************************
The following areas will be marked as having a pending
election. After the election, England, Wales, and
Northern Ireland have new IDs anyway, so those new
IDs will need to be set to recent_election when they
are put live.

@$area_ids

******************************************************
EOF
        return;
    }
    foreach (@$area_ids) {
        mySociety::DaDem::admin_set_area_status($_, 'pending_election')
    }
}

sub hearfromyourmp {
    print <<EOF;
Dear me on Monday (dissolution day 2010),

I of three days earlier can't be bothered to set up direct HearFromYourMP
access just for this, so you'll want to do something like this:

* Config: Disable the sending of threshold alerts in the crontab.
* Config: Set OPTION_POSTING_DISABLED to 1.
* DB: update constituent set is_rep='f'; -- don't want anyone being an MP any more.
* DB: Optionally, delete all MP login tokens (shouldn't matter due to above).
* DB: update message set state='closed'; -- disable comments.
* DB: Remove all the rep_nothanks table, as they might be different next time.
* Bin: Run bin/convert-subs to move people to new constituencies (after
  checking, and adding a db_commit() line).
* Deploy HearFromYourMP. 
* Check: you can still sign up (use a Hammersmith postcode to check it gives
  the correct new constituency), that the view and league pages still work
  for a variety of MPs (include Henley, Strangford, Brighton), and then
  work out what will need changing on the old view pages - easier to do that
  then than now.

Think that's about it!
EOF
    # Deploy site
}

