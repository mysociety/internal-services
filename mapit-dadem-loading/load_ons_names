#!/usr/bin/perl -w
#
# load_ons_names:
# Load names for wards etc. from the ONS Standard Names and Codes product.
#
# Call with one argument:
#   - name of SNAC Access database (.mdb file)
#
# Boundary-Line sometimes uses CAS ward codes and sometimes real ward codes. I
# don't know why. We make an effort to track down either kind, but using
# mdb-export from mdbtools to pull the relevant tables out of the SNAC
# database. Note that the table-level structure of SNAC is not documented, so
# this might break in the next release.
#
# Copyright (c) 2004 UK Citizens Online Democracy. All rights reserved.
# Email: chris@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: load_ons_names,v 1.5 2004-12-07 16:33:20 francis Exp $';

use strict;

require 5.8.0; # Shouldn't matter, but IO::Pipe seems to be broken in 5.6.x

use DBI;
use IO::File;
use IO::Pipe;
use Text::CSV_XS;

use Common;

die "argument is ONS codes CSV file" unless (@ARGV == 1);
my ($$snacdbfile) = @ARGV;

my $dbh = connect_to_mapit_database();

my %have_name = ( );

my $C = new Text::CSV_XS({ binary => 1 });

my (%casward, %realward);

my $p = (new IO::Pipe())->reader('mdb-export', $snacdbfile, 'CAS wards');
foreach (read_csv_file($p, 1)) {
    my ($code, $name) = @$_;
    $code =~ s# ##g;
    $casward{$code} = $name;
}

$p = (new IO::Pipe())->reader('mdb-export', $snacdbfile, 'UK WardNames and Codes');
foreach (read_csv_file($p, 1)) {
    my ($code, $name) = @$_;
    $code =~ s# ##g;
    $realward{$code} = $name;
}

my $s = $dbh->prepare('select id, ons_code from area where ons_code is not null');
$s->execute();

my ($only_r, $only_c, $both_differ, $both_same, $neither) = (0, 0, 0, 0, 0);

while (my ($id, $code) = $s->fetchrow_array()) {
    my $c = $casward{$code};
    my $r = $realward{$code};

    my $name;
    if (defined($c) && !defined($r)) {
        $name = $c;
        ++$only_c;
    } elsif (!defined($c) && defined($r)) {
        $name = $r;
        ++$only_r;
    } elsif (!defined($c) && !defined($r)) {
        # Probably a constituency or something where we don't have an ONS code
        # from BL.
        ++$neither;
    } elsif ($c ne $r) {
        # This shouldn't happen, I think, but it does; e.g.:
        #      code = 31UDGH
        #  CAS name = 'Broughton Astley - Primethorpe'
        # real name = 'Broughton Astley-Primethorpe'
        # Assume that the differences are all trivial.
        print STDERR "$code: CAS name = '$c'; real name = '$r'\n";
        ++$both_differ;
        $name = $c;
    } else {
        $name = $c;
        ++$both_same;
    }

    if (defined($name)) {
        $dbh->do(q#delete from area_name where area_id = ? and name_type = 'S'#, {}, $id);
        $dbh->do(q#insert into area_name (area_id, name_type, name) values (?, 'S', ?)#, {}, $id, $name);
    }
}

$dbh->commit();
$dbh->disconnect();

print STDERR "\r";

print <<EOF;
Coverage:

only real ward                      $only_r
only CAS ward                       $only_c
both real and CAS, different names  $both_differ
both real and CAS, same name        $both_same
neither                             $neither
EOF

