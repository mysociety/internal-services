#!/usr/bin/perl -w -I../../perllib
#
# LAE:
# Import representatives for area type "LAE" (i.e., London-wide GLA AMs).
#
# Copyright (c) 2004 UK Citizens Online Democracy. All rights reserved.
# Email: chris@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: LAE,v 1.1 2004-11-09 08:28:28 francis Exp $';

use strict;

use DBI;
use IO::Handle;
use Text::CSV;
use mySociety::VotingArea;

die "specify database filename as single argument\n" unless (@ARGV == 1);
my $dbname = $ARGV[0];

my $dbh = DBI->connect("dbi:SQLite:dbname=$dbname", '', '', { RaiseError => 1, AutoCommit => 0 });
#   $dbh->begin_work(); # XXX DBD::SQLite bug

$dbh->do(q#delete from representative where area_type = 'LAE'#);

my $C = new Text::CSV();

my $n = 0;
STDIN->getline();   # Skip header line
while (my $line = STDIN->getline()) {
    $n++;
    $line =~ s#\r\n#\n#;
    next unless ($line =~ /[^\s]/s);
    $C->parse($line) or die "bad CSV at line $n\n";
    my ($first, $last, $const, $party, $email, $fax) = $C->fields();
    next if ($const !~ /proportionally elected member/i);
    next if ($email =~ /mayor@/);   # XXX HACK!
    $email = undef if ($email eq '');
    $fax = undef if ($fax eq '');
    my $method = 0;
    if ($email and !$fax) {
        $method = 2;
    } elsif ($fax and !$email) {
        $method = 1;
    }
    $dbh->do(q#insert into representative (area_id, area_type, name, party, method, email, fax)
                values (?, 'LAE', ?, ?, ?, ?, ?)#, {},
                mySociety::VotingArea::LAE_AREA_ID,
                "$first $last",
                $party,
                $method,
                $email,
                $fax);
}

die "standard input: $!\n" if (STDIN->error());

$dbh->commit();
$dbh->disconnect();

=head1 NAME

LAE

=head1 SYNOPSIS

  ./LAE dadem.sqlite < gla.csv

=head1 DESCRIPTION

Update DaDem database with data about LAE representatives (i.e., London-wide
GLA assembly members). Also loads any data about the Mayor of London. Pass on
standard input a CSV file with columns,

=over 4

=item 1. first name

=item 2. surname

=item 3. constituency

The constituency name "Proportionally Elected Member" is used to indicate that
this row represents a London-wide member.

=item 4. party

=item 5. email

=item 6. fax

Either of the last two may be blank, to indicate that no such contact
information is available for this individual.

=back

