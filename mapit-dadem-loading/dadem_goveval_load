#!/usr/bin/perl -w -I../../perllib
#
# dadem_goveval_load
# Takes CSV files containing contact details for representatives from
# GovEval or similar, and loads them into DaDem.  Requires that GovEval
# names are already in the area_name table in MaPit, use
# load_goveval_name first to do that.
#
# Parameters: 
# $1 - kind of data, currently choose from: mep, mp
# stdin - the CSV file from GovEval or FaxYourMP
# make sure ../conf/general is configured for mapit db
#
# Example usage:
# cat ../../../goveval/mep-20041001.csv | ./dadem_goveval_load mep
# cat ../../../goveval/faxyourmp-mp-20041207-withcons.csv | ./dadem_goveval_load mp
#
# Copyright (c) 2004 UK Citizens Online Democracy. All rights reserved.
# Email: francis@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: dadem_goveval_load,v 1.3 2004-12-07 23:51:26 francis Exp $';

use strict;

use DBI;
use DBD::SQLite;
use Text::CSV_XS;
use Data::Dumper;
use Common;
#DBI->trace(1);

my $kind = shift(@ARGV);
die "specify what kind of CSV file" if (!defined($kind));
my $types;
my $name_type_char;
if ($kind eq "mep") {
    $types = [qw(EUR)];
} elsif ($kind eq "mp") {
    $types = [qw(WMC)];
} else {
    die "Please specify 'mep' or 'mp'";
}

my $m_dbh = connect_to_mapit_database();
my $d_dbh = connect_to_dadem_database();

$d_dbh->do(q#delete from representative where 
            (# . join(' or ', map { "area_type = '$_'" } @$types) . q#)#);

# %name_to_id
# Cache of name-->id map of all the areas
my %name_to_id;
my %name_to_type;
my $s = $m_dbh->prepare(
        q#select area_id, name, type from area_name, area
            where area_name.area_id = area.id
            and (# . join(' or ', map { "type = '$_'" } @$types) . q#)#
    );
$s->execute();
while (my ($area_id, $name, $type) = $s->fetchrow_array()) {
    #print "$area_id $name $type\n";
    die "duplicate name of differing id or type" if (exists($name_to_id{$name}) and (($name_to_id{$name} != $area_id) or ($name_to_type{$name} ne $type)));
    $name_to_id{$name} = $area_id;
    $name_to_type{$name} = $type;
}

# Read CSV file, load into dadem
my $C = new Text::CSV_XS({ binary => 1 });
<STDIN>;    # header line
while (my $line = <STDIN>) {
    chomp($line);
    $C->parse($line);
    map { die "Not valid field in $line" if (!defined $_) } $C->fields();

    my ($finalcons, $finalname, $finalparty, $finalmethod, $finalemail, $finalfax);
    if ($kind eq "mep") {
        my ($first, $last, $constituency, $party, $email, $fax) = map { trim_spaces($_) } $C->fields();

        $finalcons = $constituency;
        $finalname = "$first $last";
        $finalparty = $party;

        $finalemail = $email;
        $finalfax = $fax;
        $finalmethod = 0;

    } elsif ($kind eq "mp") {
        my ($name, $constituency, $email, $fax, $phone, $constituencyfax, $party) = map { trim_spaces($_) } $C->fields();

        $finalcons = $constituency;
        $finalname = $name;
        $finalparty = $party;

        $finalemail = $email;
        $finalemail = undef if ($email eq "");
        $finalemail = undef if ($email eq "NULL");
        $finalemail = undef if ($email eq "shame");
        $finalemail = undef if ($email eq "byelection");
        $finalfax = $fax;
        $finalfax = undef if ($fax eq "");
        $finalfax = undef if ($fax eq "NULL");
        $finalfax = $constituencyfax if (!defined($finalfax));

        if (defined($finalemail)) {
            $finalmethod = 2; # email
        } else {
            $finalmethod = 1; # fax
        }
    } else {
        die "Missing kind in code";
    }

    die "unmatched GovEval name '$finalcons', not in database" unless (exists($name_to_id{$finalcons}));
    my $area_id = $name_to_id{$finalcons};
    my $area_type = $name_to_type{$finalcons};

#    print "insert into representative (area_id, area_type, name,
#        party, method, email, fax) values (?, ?, ?, ?, ?, ?, ?)",
#        $area_id, " ", $area_type, " ", $finalname, " ", $finalparty, "
#        ", $finalmethod, " ", $finalemail, " ", $finalfax;
#    print "\n";

    $d_dbh->do("insert into representative (area_id, area_type, name,
        party, method, email, fax) values (?, ?, ?, ?, ?, ?, ?)", {},
        $area_id, $area_type, $finalname, $finalparty, $finalmethod,
        $finalemail, $finalfax);
}
 
$m_dbh->disconnect();
$d_dbh->commit();
$d_dbh->disconnect();

