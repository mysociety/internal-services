#!/usr/bin/perl -w -I../perllib -I ../../perllib
#
# 2009-12-derryaghy:
# SI 2008/1486 remade all the Northern Ireland Parliamentary Constituencies.
# Derryaghy ward was split in two, between Belfast West and Lagan Valley.
# This script tries to sort that out, given we don't have the geometry.
# It uses a magic CSV file that I constructed BY HAND.
#
# Copyright (c) 2009 UK Citizens Online Democracy. All rights reserved.
# Email: matthew@mysociety.org; WWW: http://www.mysociety.org/
#
# $Id: 2009-12-derryaghy,v 1.1 2009-12-24 02:46:34 matthew Exp $

use strict;
use Common;
use mySociety::GeoUtil;

my $dbh = Common::connect_to_mapit_database();
my $generation = $dbh->selectrow_array('select id from new_generation');

my (%cid, $cname);
$cname = 'Lagan Valley'; $cid{$cname} = fetch_id($cname);
$cname = 'Belfast West'; $cid{$cname} = fetch_id($cname);

my $check = $dbh->prepare("
    select postcode_id from postcode_area
        where postcode_id = ? and area_id = ?");

open(FP, 'Derryaghy-postcodes.csv') or die $!;
while (<FP>) {
    chomp;
    my ($pc, $constituency) = split /,/;
    my $other;
    $other = 'Belfast West' if $constituency eq 'Lagan Valley';
    $other = 'Lagan Valley' if $constituency eq 'Belfast West';
    $pc =~ s/ //g;
    my $pcid = $dbh->selectrow_array('select id from postcode where postcode = ?', {}, $pc);
    die "Postcode not found: $pc" unless $pcid;

    $check->execute($pcid, $cid{$other});
    $dbh->do('delete from postcode_area where postcode_id=? and area_id=?', {}, $pcid, $cid{$other})
        if $check->fetchrow_array();

    $check->execute($pcid, $cid{$constituency});
    $dbh->do('insert into postcode_area (postcode_id, area_id) values (?, ?)', {}, $pcid, $cid{$constituency})
        if (!$check->fetchrow_array);

}
close(FP);

$check->finish();
print STDERR "committing changes... ";
$dbh->commit();
$dbh->disconnect();
print STDERR "done.\n";

sub fetch_id {
    my $cname = shift;
    my $const_id = $dbh->selectrow_array("select id from area, area_name where id=area_id and name_type='F' and name=? and generation_high=?", {}, $cname, $generation);
    die unless $const_id;
    return $const_id;
}

